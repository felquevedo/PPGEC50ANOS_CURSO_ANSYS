!
! EPD_ELIPTICA_MALHA_ANSYS
!
FINISH 
/CLEAR,NOSTART
!
! Parametros geométricos
Rh		= 1		! [m] raio horizontal externo do túnel
Rv		= 1		! [m] raio vertical externo do túnel
R1		= 10*Rv		! [m] raio da região de refinamento da malha próxima do túnel
Lx		= 20*Rv		! [m] dimensão da base do domínio
Ly		= 20*Rv		! [m] dimensão da altura do domínio
!
! Material maciço
E1 		= 50		! [kN/m²] modulo elasticidade
nu1 		= 0.498		! [adm] coeficiente de poisson
!
! Parametros das condições de contorno
pi		= 0		! [kN/m²] pressão interna
pv		= 0.5		! [kN/m²] pressão vertical
ph		= 0.5		! [kN/m²] pressão horizontal
!
! Parametros para discretização
nre		= 10		! [un] divisões na interface entre o túnel e o maciço
nr1		= 15		! [un] divisões ao longo do raio da região de refinamento
mr1		= 15		! [%] taxa de crescimento de nr1
nx1		= 5		! [un] divisões na base fora da região de refinamento
mx1		= 1.2		! [%] taxa de crescimento de mx1
ny1		= nx1		! [un] divisões na altura fora da região de refinamento
my1		= mx1		! [%] taxa de crescimento de my1
nrv		= nre/2		! [un] divisões na vertical da parte interna da seção do túnel
nrh		= nre/2		! [un] divisões na horizontal da parte interna da seção do túnel
!
! **************************************************************************
/PREP7
!
! Parametros calculados
ratio		= Rv/Rh		
Lx1		= Lx-R1		
Ly1		= Ly-R1
!
! Elemento finito
ET,1,PLANE183
KEYOPT,1,1,0
KEYOPT,1,3,2
KEYOPT,1,6,0
!
! Material do maciço
MPTEMP,1,0 
MPDATA,EX,1,,E1
MPDATA,PRXY,1,,nu1
MPDATA,DENS,1,,dens1 
!
! Keypoints do modelo geométrico
K,1,0,0
K,2,Rh,0
K,3,R1,0
K,4,Lx,0
K,5,0,Rv
K,6,0,R1,
K,7,0,Ly
K,8,Lx,Ly
!
! Criando linhas
L,1,2,nRh
L,2,3,nR1,mR1
L,3,4,ny1,my1
LOCAL,11,1,,,,,,,ratio,
L,5,2,nRe
LOCAL,11,1,,,,,,,1,
L,6,3,nRe
LOCAL,11,0
L,7,8,nRe/2
L,1,5,nRv
L,5,6,nR1,mR1
L,6,7,ny1,my1
L,4,8,nRe/2
!
! Criando áreas
AL,2,4,8,5
AL,3,5,9,6,10
!
! Criando malha
ALLSEL,ALL
LSEL,S,LINE,,6
LSEL,A,LINE,,10
LCCAT,ALL
!
ALLSEL,ALL
MSHAPE,0,2D 
AMESH,ALL
!
! Condição de contorno de simetria
ALLSEL,ALL
LSEL,S,LINE,,8
LSEL,A,LINE,,9
LSEL,A,LINE,,2
LSEL,A,LINE,,3
DL,ALL, ,SYMM
!
! Condição de contorno de carga
ALLSEL,ALL
LSEL,S,LINE,,6
SFL,ALL,PRESS,pv 
!
ALLSEL,ALL
LSEL,S,LINE,,10
SFL,ALL,PRESS,ph 
!
ALLSEL,ALL
LSEL,S,LINE,,4
SFL,ALL,PRESS,pi
!
! **************************************************************************
! Processamento
/SOLU
ALLSEL,ALL
SOLVE   
FINISH 
!
! **************************************************************************
! Pos-processamento
/POST1  
RSYS,1
/DSCALE,ALL,1.0 
!
! Campo de deformações em rr
PLNSOL, U,X, 0,1.0  
!
! Cria caminho
ALLSEL,ALL
NSEL,S,LOC,X,Rh
NSEL,R,LOC,Y,0
*GET,no1,NODE,0,NUM,MAX
!
ALLSEL,ALL
NSEL,S,LOC,X,Lx
NSEL,R,LOC,Y,0
*GET,no2,NODE,0,NUM,MAX
!
ALLSEL,ALL
PATH,caminho,2,30,20,   
PPATH,1,no1
PPATH,2,no2
PDEF, ,U,X,AVG
PDEF, ,S,X,AVG
PDEF, ,S,Y,AVG
PDEF, ,S,Z,AVG
!
! Plota resultados
/PBC,PATH, ,0    
PLPAGM,UX,30,'NODE' 
PLPAGM,SX,30,'NODE' 
PLPAGM,SY,30,'NODE' 
PLPAGM,SZ,30,'NODE' 